---
title: Flake et Home Manager
description: ""
date: 2025-09-06T04:47:11.160Z
preview: ""
tags:
  - IDE
  - distribution
categories:
  - nixos
layout: post
subtitle: Un guide pratique pour débuter avec flake
author: Damien
comments: false
mathjax: false
draft: false
---


Dans cet article, je vais vous expliquer la migration d'une configuration NixOS avec l'utilisation de home-manager + flake

NixOS est déclaré dans 2 fichiers:
- configuration.nix, qui décrit, les paramètre systèmes, les programmes et configurations
- hardware-configuration.nix, généré à l'installation, il décrit principalement les partitions et l'amorçage

## Flake

Un flake est un mécanisme introduit dans Nix pour rendre les configurations reproductibles, composables et partageables. Il fournit une structure standardisée qui permet de définir des dépendances, des environnements. Avec Flake on peut gérer plusieurs configurations Nix. La structure est composée de 2 fichiers: 
- flake.nix pour la déclaration
- flake.lock généré automatiquement pour la version des dépendances.

```sh
├── flake.lock
├── flake.nix
```

## Home manager

Home manager est un outil qui permet de gérer l’environnement utilisateur de façon déclarative. Il reprend le principe de configuration.nix en ajoutant la possibilité de gérer des paramètres personnels à l'utilisateur et les dotfiles.
Il est indépendant de la configuration système.

Le meilleur des deux mondes ? Home Manager dans un flake

## Bien débuter avec Flake

Pour commencer je recommande vivement l'utilisation d'un IDE pour l'édition des fichiers nix.  
J'apprécie particulèrement vscodium.

Dans le cas d'un utilisateur qui souhaite convertir son intallation actuel vers flake, je recommande d'utiliser une structure de dossier. Ce [projet github](https://github.com/Misterio77/nix-starter-configs) reconnu, propose 2 versions:
- minimal
- standard, la version choisie pour la suite

L'avantage de cette structure, c'est la décomposition de la configuration en modules.  

## Etapes de migration

- Créer un dossier pour stocker la configuration flake
- Importer la structure choisie
- Remplacer ~/nixos/hardware-configuration.nix par le votre
- Configurer ~/nixos/configuration.nix
- Créer les modules nixos
- Configurer ~/home-manager/home.nix
- Créer les modules home-manager
- Appliquer les configurations

### Schéma

![](/assets/img/nixosmigration.svg)

### Import de la nouvelle structure

```sh
mkdir config
cd config
nix flake init -t github:misterio77/nix-starter-config#standard
```

Structure crée
```sh
.
├── flake.lock
├── flake.nix
├── home-manager
│   └── home.nix
├── modules
│   ├── home-manager
│   │   └── default.nix
│   └── nixos
│       └── default.nix
├── nixos
│   ├── configuration.nix
│   └── hardware-configuration.nix
├── overlays
│   └── default.nix
└── pkgs
    └── default.nix
```

### Copie de hardware-configuration.nix

```sh
cp /etc/nixos/hardware-configuration.nix ~/config/nixos/hardware-configuration.nix
```

### Configuration /nixos/configuration.nix:

J'ai pris le fichier fourni dans le starter kit et je l'ai épuré au maximum.  
**Ne pas oublier de changer la version originale de nixos en bas de fichier.**  

```nix
{ inputs, outputs, lib, config, pkgs, ... }: {
  imports = [
    # Ici on déclarera les modules/nixos

    ./hardware-configuration.nix # Déclaration du matériel
  ];

  nixpkgs = {
    overlays = [
      # Ici on déclarera les overlays
      outputs.overlays.additions
      outputs.overlays.modifications
      outputs.overlays.unstable-packages
    ];
    config = { allowUnfree = true; };
  };

  nix = let flakeInputs = lib.filterAttrs (_: lib.isType "flake") inputs;
  in {
    settings = {
      experimental-features = "nix-command flakes";
      flake-registry = "";
      nix-path = config.nix.nixPath;
      auto-optimise-store = true;
    };
    channel.enable = false;
    registry = lib.mapAttrs (_: flake: { inherit flake; }) flakeInputs;
    nixPath = lib.mapAttrsToList (n: _: "${n}=flake:${n}") flakeInputs;
  };

  networking.hostName = "nixos";  # changer le nom d'hôte

  # Original version of nixos
  system.stateVersion = "24.11";
}
```

Dans un premier temps je m'occupe uniquement des modules.

### Créer les modules nixos

Dans l'étape précédente on a crée un configuration.nix minimale.  
On peut se contenter de copier le reste dans configuration.nix comme dans le fichier original mais je préfère utiliser des modules pour organiser les configurations.  
Les modules sont déjà intégrés à Nixos, les flakes permettent de les structurer, partager entre différentes configurations.  
On verra par la suite que ces modules sont très utiles pour les configurations multi utilisateurs et multi configurations.

Répertoire de travail: ~/modules/nixos

Le fichier default.nix permet de déclarer tous les fichiers présents dans ce répertoire.

Exemple de fichier default.nix pour la syntaxe:
```nix
{
  audio = import ./audio.nix;
  bluetooth = import ./bluetooth.nix;
  boot = import ./boot.nix;
  DE = import ./DE.nix;
  DM = import ./DM.nix;
  drivers = import ./drivers.nix;
  font = import ./font.nix;
  fstab = import ./fstab.nix;
  locale = import ./locale.nix;
  system_programs = import ./system_programs.nix;
  system_services = import ./system_services.nix;
  users = import ./users.nix;
}
```
Je conseille de garder cette liste dans le même ordre que votre répertoire ~/modules/nixos pour plus de lisibilité.

Exemple de module audio.nix pour la syntaxe
```nix
{ config, pkgs, lib, ... }:

{
  options = {};
  config = {
    # Enable sound with pipewire.
    services.pulseaudio.enable = false;
    security.rtkit.enable = true;
    services.pipewire = {
      enable = true;
      alsa.enable = true;
      alsa.support32Bit = true;
      pulse.enable = true;
    };
  };
}
```

Les modules déclarés, il faut maintenant les ajouter dans ~/nixos/configuration.nix
```nix
{ inputs, outputs, lib, config, pkgs, ... }: {
  imports = [
    outputs.nixosModules.audio
    outputs.nixosModules.bluetooth
    outputs.nixosModules.boot
    outputs.nixosModules.DE
    outputs.nixosModules.DM
    outputs.nixosModules.docker
    outputs.nixosModules.drivers
    outputs.nixosModules.font
    outputs.nixosModules.fstab
    outputs.nixosModules.locale
    outputs.nixosModules.system_programs
    outputs.nixosModules.system_services
    outputs.nixosModules.users
    ./hardware-configuration.nix # Déclaration du matériel
  ];

<SNIP>  
```

Pour désactiver temporairement un module il suffira de commenter la ligne avec "#".

Vous pouvez maintenant décomposer votre configuration.nix original en une multitude de  modules, à l'exception de tout ce qui concerne la partie utilisateur qui sera géré par Home manager:
- répertoire /home
- les programmes utilisateur
- les fichiers de configuration dit " dotfiles".
  
Home-manager nous permettra de déclarer cette partie.

### Configuration ~/home-manager/home.nix

A l'identique de configuration.nix, ci-desous un exemple de home.nix épuré

```nix
{ inputs, outputs, lib, config, pkgs, ... }: {
  imports = [
    # Ici on déclarera les modules/home-manager

  ];

  nixpkgs = {
    # Ici on déclarera les overlays
    overlays = [
      outputs.overlays.additions
      outputs.overlays.modifications
      outputs.overlays.unstable-packages

    ];
    config = { allowUnfree = true; };
  };

  # Modifier le nom d'utilisateur
  home = {
    username = "mon_utilisateur";
    homeDirectory = "/home/mon_utilisateur";
  };

  home.packages = with pkgs; [
    # Déclaration des paquets utilisateur

  ];

  programs.home-manager.enable = true;

  # Nicely reload system units when changing configs
  systemd.user.startServices = "sd-switch";

  # Version de home manager initiale
  home.stateVersion = "25.05";
}
```

### Créer les modules home manager

Le principe est le même que pour les modules nixos, les modules ont la même syntaxe et structure et on déclarera default.nix

Répertoire de travail: ~/modules/home-manager

On déclare dans ~/config/home-manager/home.nix les modules ajoutés, exemple

```nix
{ inputs, outputs, lib, config, pkgs, ... }: {
  imports = [
    # Ici on déclarera les modules/home-manager
    outputs.homeManagerModules.alacritty
    outputs.homeManagerModules.bashrc
    outputs.homeManagerModules.brave
    outputs.homeManagerModules.chromium
    outputs.homeManagerModules.fastfetch
    outputs.homeManagerModules.font
    outputs.homeManagerModules.mako
    outputs.homeManagerModules.niri
    outputs.homeManagerModules.rofi
    outputs.homeManagerModules.vscodium
  ];

<SNIP>
```

Pour désactiver temporairement un module il suffira de commenter la ligne avec "#".

### Déterminer si un paquet est système ou utilisateur

Un paquet est à déclarer coté système si un des critères suivant:
- besoin des droits root
- utilisation pour tous les utilisateurs du système, exemple un service

Un paquet est à déclarer coté utilisateur si:
- droits utilisateur seulement
- besoin spécifique

Pour connaitre le nom d'un paquet, d'une option, je recommande [searchix](https://searchix.ovh/)

### Appliquer la nouvelle configuration

Dans votre répertoire de travail

Nixos avec flake
```nix
# Tester
sudo nixos-rebuild build --flake .#hôte

# Appliquer
sudo nixos-rebuild switch --flake .#hôte
```

Home manager avec flake
```nix
# Tester
home-manager build --flake .#utilisateur@hôte

# Appliquer
home-manager switch --flake .#utilisateur@hôte
```

Un comportement inattendu ? l'avantage de Nixos c'est qu'il vous suffira de redémarrer et choisir une génération précédente pour revenir en arrière.